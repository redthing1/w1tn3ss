# compiler cache selection for w1tn3ss
include_guard()

if(NOT W1_ENABLE_COMPILER_CACHE)
    return()
endif()

if(CMAKE_C_COMPILER_LAUNCHER OR CMAKE_CXX_COMPILER_LAUNCHER)
    message(STATUS "Compiler launcher already set; skipping w1 cache")
    return()
endif()

set(_w1_cache "")
if(WIN32)
    find_program(_w1_cache sccache)
    if(NOT _w1_cache)
        find_program(_w1_cache ccache)
    endif()
else()
    find_program(_w1_cache ccache)
    if(NOT _w1_cache)
        find_program(_w1_cache sccache)
    endif()
endif()

if(_w1_cache)
    set(CMAKE_C_COMPILER_LAUNCHER "${_w1_cache}" CACHE STRING "C compiler cache" FORCE)
    set(CMAKE_CXX_COMPILER_LAUNCHER "${_w1_cache}" CACHE STRING "CXX compiler cache" FORCE)
    set(QBDI_CCACHE OFF CACHE BOOL "Enable QBDI ccache" FORCE)
    set(LIEF_USE_CCACHE OFF CACHE BOOL "Use ccache for LIEF" FORCE)
    message(STATUS "Compiler cache: ${_w1_cache}")
else()
    message(STATUS "Compiler cache requested but not found")
endif()
