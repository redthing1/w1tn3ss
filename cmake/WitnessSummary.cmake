# w1tn3ss configuration summary
include_guard()

function(witness_collect_enabled_components out_var)
    set(_enabled "")
    if(WITNESS_COMPONENTS)
        foreach(component IN LISTS WITNESS_COMPONENTS)
            string(TOUPPER "${component}" _upper)
            string(REPLACE "-" "_" _upper "${_upper}")
            set(_opt "WITNESS_BUILD_${_upper}")
            if(DEFINED ${_opt} AND ${_opt})
                list(APPEND _enabled "${component}")
            endif()
        endforeach()
    endif()

    if(NOT _enabled)
        set(_enabled "(none)")
    else()
        list(JOIN _enabled ", " _enabled)
    endif()

    set(${out_var} "${_enabled}" PARENT_SCOPE)
endfunction()

function(witness_has_zstd out_var)
    set(_has_zstd OFF)
    if(TARGET w1_zstd)
        get_target_property(_defs w1_zstd INTERFACE_COMPILE_DEFINITIONS)
        if(NOT _defs)
            set(_defs "")
        endif()
        foreach(_def IN LISTS _defs)
            if(_def STREQUAL "WITNESS_REWIND_HAVE_ZSTD=1")
                set(_has_zstd ON)
                break()
            endif()
        endforeach()
    endif()

    set(${out_var} ${_has_zstd} PARENT_SCOPE)
endfunction()

function(witness_print_summary)
    witness_collect_enabled_components(_enabled_components)
    witness_has_zstd(_has_zstd)

    if(CMAKE_CONFIGURATION_TYPES)
        list(JOIN CMAKE_CONFIGURATION_TYPES ", " _config_types)
        set(_build_type "${_config_types}")
    else()
        if(CMAKE_BUILD_TYPE)
            set(_build_type "${CMAKE_BUILD_TYPE}")
        else()
            set(_build_type "(default)")
        endif()
    endif()

    if(TARGET w1_zstd)
        if(_has_zstd)
            set(_zstd_status "found")
        else()
            set(_zstd_status "not found")
        endif()
    else()
        set(_zstd_status "not checked")
    endif()

    if(WITNESS_SCRIPT)
        set(_script_engine "${WITNESS_SCRIPT_ENGINE}")
    else()
        set(_script_engine "(disabled)")
    endif()

    message(STATUS "")
    message(STATUS "w1tn3ss configuration:")
    message(STATUS "  Generator: ${CMAKE_GENERATOR}")
    message(STATUS "  Build type: ${_build_type}")
    message(STATUS "  Compiler: ${CMAKE_CXX_COMPILER_ID} ${CMAKE_CXX_COMPILER_VERSION}")
    message(STATUS "  Components: ${_enabled_components}")
    message(STATUS "  Build static: ${WITNESS_BUILD_STATIC}")
    message(STATUS "  Build shared: ${WITNESS_BUILD_SHARED}")
    message(STATUS "  Scripting: ${WITNESS_SCRIPT} (${_script_engine})")
    message(STATUS "  LIEF: ${WITNESS_LIEF}")
    message(STATUS "  ASMR: ${WITNESS_ASMR}")
    message(STATUS "  Use system deps: ${WITNESS_USE_SYSTEM_DEPS}")
    message(STATUS "  Zstd: ${_zstd_status} (require=${WITNESS_REQUIRE_ZSTD}, system=${WITNESS_USE_SYSTEM_ZSTD}, prefer_static=${WITNESS_PREFER_STATIC_ZSTD})")
    message(STATUS "  Dev tools: ${WITNESS_ENABLE_DEV_TOOLS} (clang-tidy=${WITNESS_ENABLE_CLANG_TIDY})")
    message(STATUS "  Packaging: ${WITNESS_ENABLE_PACKAGING}")
    message(STATUS "  Compiler cache: ${WITNESS_ENABLE_COMPILER_CACHE}")
    message(STATUS "  Export compile_commands: ${WITNESS_EXPORT_COMPILE_COMMANDS}")
    if(DEFINED WITNESS_ARCH)
        message(STATUS "  Architecture: ${WITNESS_ARCH}")
    endif()
    message(STATUS "  Output dirs: bin=${WITNESS_OUTPUT_BIN_DIR}, lib=${WITNESS_OUTPUT_LIB_DIR}, test=${WITNESS_OUTPUT_TEST_DIR}, samples=${WITNESS_OUTPUT_SAMPLE_DIR}")
    message(STATUS "  Install component: ${WITNESS_INSTALL_COMPONENT} (tests=${WITNESS_TESTS_COMPONENT})")
    message(STATUS "")
endfunction()
